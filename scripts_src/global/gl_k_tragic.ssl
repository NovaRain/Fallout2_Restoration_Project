#define SCRIPT_REALNAME "gl_k_tragic"
#include "../headers/define.h"
#include "../headers/scripts.h"
#include "../headers/command.h"

#define TIME_TRAGIC_USAGE ONE_GAME_DAY
#define TIME_DURATION_OF_ADDICTION ONE_GAME_WEEK
#define INTERFACE_TAG_ADDICT 4

#define MSG_FILE "k_tragic.msg"
#define MSG_craving 1000
#define MSG_relieved 1001

/**
 * Displays message `x` from **game** message file `msg`
 * @arg {int} x - message number
 */
#define display_mymsg(x) display_msg(message_str_game(msg, x))

procedure start;

variable time_of_last_craving;
variable time_of_game_load;
variable msg;

procedure start begin
    if is_loading_game then return;

	if game_loaded then begin
	  	set_global_script_repeat(60);
		set_global_script_type(3);
        msg = add_extra_msg_file(MSG_FILE);

        time_of_game_load = game_time;
        time_of_last_craving = game_time;
        return;
	end

    // No addiction
    if global_var(GVAR_ADDICT_TRAGIC) == 0 then return;

    // Craving
    if game_time - time_of_last_craving >= TIME_TRAGIC_USAGE then begin
        if in_world_map then force_encounter(0);

        time_of_last_craving = game_time;
        if (game_time - time_of_game_load < TIME_DURATION_OF_ADDICTION) then display_mymsg(MSG_craving);
    end

    // Addiction passed
    if game_time - time_of_game_load >= TIME_DURATION_OF_ADDICTION then begin
        set_global_var(GVAR_ADDICT_TRAGIC, 0);
        if not dude_is_addicted() then hide_iface_tag(INTERFACE_TAG_ADDICT);
        display_mymsg(MSG_relieved);
    end
end
